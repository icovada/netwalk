Value Required name (\S+)
Value is_enabled (.+?)
Value is_up (.+?)
Value hardware_type ([\w ]+)
Value address ([a-fA-F0-9]{4}\.[a-fA-F0-9]{4}\.[a-fA-F0-9]{4})
Value bia ([a-fA-F0-9]{4}\.[a-fA-F0-9]{4}\.[a-fA-F0-9]{4})
Value description (.+?)
Value mtu (\d+)
Value duplex (([Ff]ull|[Aa]uto|[Hh]alf|[Aa]-).*?)
Value speed (.*?)
Value media_type (\S+(\s+\S+)?)
Value bandwidth (\d+\s+\w+)
Value delay (\d+\s+\S+)
Value encapsulation (.+?)
Value last_in (.+?)
Value last_out (.+?)
Value last_out_hang (.+?)
Value last_clearing (.*)
Value queue_strategy (.+)
Value input_rate (\d+)
Value output_rate (\d+)
Value input_packets (\d+)
Value output_packets (\d+)
Value input_errors (\d+)
Value crc (\d+)
Value abort (\d+)
Value output_errors (\d+)

Start
  ^\S+\s+is\s+.+?,\s+line\s+protocol.*$$ -> Continue.Record
  ^${name}\s+is\s+${is_enabled},\s+line\s+protocol\s+is\s+${is_up}\s*$$
  ^\s+Hardware\s+is\s+${hardware_type} -> Continue
  ^.+address\s+is\s+${address}\s+\(bia\s+${bia}\)\s*$$
  ^\s+Description:\s+${description}\s*$$
  ^\s+Internet\s+address\s+is.+$$
  ^\s+MTU\s+${mtu}.*BW\s+${bandwidth}.*DLY\s+${delay},\s*$$
  ^\s+Encapsulation\s+${encapsulation},.+$$
  ^\s+Last\s+input\s+${last_in},\s+output\s+${last_out},\s+output\s+hang\s+${last_out_hang}\s*$$
  ^\s+Last\s+clearing\s+of\s+"show interface"\s+counters\s+${last_clearing}$$
  ^\s+Queueing\s+strategy:\s+${queue_strategy}\s*$$
  ^\s+${duplex},\s+${speed},.+media\stype\sis\s${media_type}$$
  ^.*input\s+rate\s+${input_rate}.+$$
  ^.*output\s+rate\s+${output_rate}.+$$
  ^\s+${input_packets}\s+packets\s+input,\s+\d+\s+bytes,\s+\d+\s+no\s+buffer\s*$$
  ^\s+${input_errors}\s+input\s+errors,\s+${crc}\s+CRC,\s+\d+\s+frame,\s+\d+\s+overrun,\s+\d+\s+ignored\s*$$
  ^\s+${input_errors}\s+input\s+errors,\s+${crc}\s+CRC,\s+\d+\s+frame,\s+\d+\s+overrun,\s+\d+\s+ignored,\s+${abort}\s+abort\s*$$
  ^\s+${output_packets}\s+packets\s+output,\s+\d+\s+bytes,\s+\d+\s+underruns\s*$$
  ^\s+${output_errors}\s+output\s+errors,\s+\d+\s+collisions,\s+\d+\s+interface\s+resets\s*$$
  # Capture time-stamp if vty line has command time-stamping turned on
  ^Load\s+for\s+
  ^Time\s+source\s+is