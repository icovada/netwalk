Value Filldown local_host (\S+)
Value local_port (\S+)
Value chassis_id (\S+)
Value remote_port_id (.*)
Value remote_port (.*)
Value neighbor (.+?)
Value system_description (.*)
Value capabilities (.*)
Value mgmt_ip (\S+)
Value vlan (\d+)
Value serial (\S+)

Start
  ^.*not advertised
  ^.*Invalid input detected -> EOF
  ^.*LLDP is not enabled -> EOF
  ^Local\s+Intf:\s+${local_port}\s*$$
  ^Chassis\s+id:\s+${chassis_id}\s*$$
  ^Port\s+id:\s+${remote_port_id}\s*$$
  ^Port\s+Description:\s+${remote_port}\s*$$
  ^System\s+Name(\s+-\s+not\s+advertised)\s*$$
  ^System\s+Name:?\s*$$
  ^System\s+Name(:\s+${neighbor})\s*$$
  ^System\s+Description -> GetDescription
  ^Time
  ^System\s+Capabilities
  ^Enabled\s+Capabilities:\s+${capabilities}\s*$$
  ^Management\s+Addresses
  ^\s+OID
  ^\s+[\d+\.]{8,}
  ^.*IP:\s+${mgmt_ip}
  ^Auto\s+Negotiation
  ^Physical\s+media
  # Removed \(\s+\) from the template - The line 'Other/unknown' would not be captured
  # Now looks for any text beginning with any space
  ^\s+.+\s*$$
  ^Media\s+Attachment
  ^\s+Inventory
  ^\s+Capabilities
  ^\s+Device\s+type
  ^\s+Network\s+Policies
  ^\s+Power\s+requirements
  ^\s+Location
  ^Time\s+remaining
  ^Vlan\s+ID:\s+(?:${vlan}|-\s+not\s+advertised)\s*$$
  ^\s+\(\S+\)
  ^(?:PoE|\s+Power)
  ^\s*-+\s*$$ -> Record
  ^MED -> Med
  ^\s*\^\s*
  ^\s*Total\s+entries\s+displayed -> Record
  ^\s*$$
  # Capture time-stamp if vty line has command time-stamping turned on
  ^Load\s+for\s+
  ^Time\s+source\s+is

GetDescription
  ^${system_description} -> IgnoreDescription

IgnoreDescription
  ^Time\s+remaining -> Start
  ^\S*
  ^\s*$$
  ^.* -> Error

Med
  ^\s+Serial\s+number:\s+${serial}
  ^\s+\S+
  ^\s*$$
  ^\s*Total\s+entries\s+displayed -> Record
  ^\s*-+\s*$$ -> Continue.Record
  ^\s*-+\s*$$ -> Start
  ^.* -> Error